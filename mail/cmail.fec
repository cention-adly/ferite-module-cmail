uses "cmail.lib";
uses "array";
uses "regexp";
uses "string";
uses "iconv";
uses "filesystem";

module-header {
	#include "imap_utility.h"
	#include <imap/linkage.h>
	#include <imap/c-client.h>

	extern char *curhst;
	extern char *curusr;
	extern char *global_pwd;
	#define SelfObj ((MAILSTREAM*)(self->odata))
}

/**
 * @namespace Mail
 * @brief     The holding namespace for Mail related tools
 * @example <code>
uses "cmail";
<nl/>uses "console";
<nl/>
<nl/>function fetchmail( string host, string username, string password ) {
<nl/><tab/>object connection = new Mail.Connection();
<nl/>
<nl/><tab/>if( connection.openMailbox( "{$host:993/imap/ssl/novalidate-cert/user=\"$username\"}", password) ) {
<nl/><tab/><tab/>number count = connection.messageCount();
<nl/><tab/><tab/>for( number i = 1; i <= count; i++ ) {
<nl/><tab/><tab/><tab/>object message = connection.messageAtIndex(i);
<nl/><tab/><tab/><tab/>Console.println("Got message: ${message.getSubject()}: ${message.toText()}");
<nl/><tab/><tab/>}
<nl/><tab/>}
<nl/>}
<nl/>
<nl/>fetchmail( "mail.myisp.com", "username", "password" );</code>
 */
namespace modifies Mail {

	/**
	 * @variable defaultEncodingTarget
	 * @type     string
	 * @brief    The default encoding required for email contents. The Mail module will convert all messages to 
                 a default encoding to make handling of messages easier. The default and recommended default encoding
                 is UTF-8. 
	 */
	string defaultEncodingTarget = "UTF-8";
	/**
	 * @variable defaultEncodingSource
	 * @type     string
	 * @brief    The default encoding assumed when emails do not provide a default encoding.
	 */
	string defaultEncodingSource = "ISO-8859-1";
	
	/**
	 * @function convertToDefaultEncodingFrom
	 * @declaration function convertToDefaultEncodingFrom( string content, string type )
	 * @brief Convert a given string from a type to the default encoding
	 * @param string content The content to convert
	 * @param string type The source character set type
	 * @return The converted string on success
	 */
	function convertToDefaultEncodingFrom( string content, string type ) {
	    object convertor = null;

	    monitor {
			convertor = new iconv.Convertor(Mail.defaultEncodingTarget, (type ? type : .defaultEncodingSource));
	    } handle {
			type = ""; //no convertion beacasue we have no convertor..... :/
	    }
		if( not type ) {
			Console.println("module.mail: No charset requested, defaulting to ${.defaultEncodingSource}");
		}
		if( not (type.toLower() == .defaultEncodingTarget.toLower()) ) {
			monitor {
				return convertor.convert(content);
			} handle {
				if( [ 'us-ascii' ].valueExists(type.toLower()) ) {
					Console.println("module.module: $type failed, trying ${.defaultEncodingSource}");
					return .convertToDefaultEncodingFrom( content, .defaultEncodingSource );
				}
				Console.println("module.mail: Error Converting from $type to ${Mail.defaultEncodingTarget}");
				return content;
			}
		}
		return content;
	}
	/**
	 * @function canDecodeISOString
	 * @declaration function canDecodeISOString( string str )
	 * @brief Check to see if a given string can be converted from the ISO email format to plain text
	 * @param string str The string to check
	 * @return true if the string can be fixed, false otherwise
	 */
	function canDecodeISOString( string str ) {
		object o = new Regexp( '[=?]+(iso-.*?|utf-8|windows-.*?|x-unknown|us-ascii)[?](.)[?](.*?)\?=', "i" );
		object o2 = new Regexp( '(iso-.*?)[\']{2}(.*)', "i" );

		str = Regexp.replaceAll( '\n', str, '' );
		if( o.match( str ) or o2.match( str ) ) {
			return true;
		}
		return false;
	}
	/**
	 * @function decodeISOString
	 * @declaration function decodeISOString( string str )
	 * @brief Convert a string from the ISO format to a plain text string
	 * @param string str The string to convert
	 * @return The converted string
	 */
	function decodeISOString( string str ) {
		string nstr;
		object o = new Regexp( '[=?]+(iso-.*?|utf-8|windows-.*?|x-unknown|us-ascii)[?](.)[?](.*?)\?=', "i" );
		object o2 = new Regexp( '(iso-.*?)[\']{2}(.*)', "i" );
		number currentIndex = 0;

		str = Regexp.replaceAll( '\n', str, '' );
		if( o.match( str ) ) {
			str = Regexp.replaceAll( '_', str, ' ' );
			o.matchAll( str ) using ( match ) {
				if( match.span()[0] != currentIndex )
					nstr += str[currentIndex..(match.span()[0]-1)];

				if( String.compareNoCase(match.capture(1), "q") )
					nstr += .convertToDefaultEncodingFrom(Mail.Connection.qprint( match.capture(2) ), match.capture(0));
				else if( String.compareNoCase( match.capture(1), "b") )
					nstr += .convertToDefaultEncodingFrom(Mail.Connection.base64( match.capture(2) ), match.capture(0));
				else
					nstr += .convertToDefaultEncodingFrom(match.capture(2), match,capture(0));

				currentIndex = match.span()[1];
			};

			if( currentIndex < String.length(str) )
				nstr += str[currentIndex..];

			return nstr;
		}
		else if( o2.match( str ) ) {
			object match = null;
			str = Regexp.replaceAll( '_', str, ' ' );
			match = o2.match( str );
			str = Regexp.replaceAll( '%', str, '=' );
			return .convertToDefaultEncodingFrom(Mail.Connection.qprint(match.capture(1)),match.capture(0));
		}
		return str;
	}
	/**
	 * @function encodeISOString
	 * @declaration function encodeISOString( string str )
	 * @brief ISO encode a given string, this function assumes the strings character set is that of the default target
	 * @param string str The string to encode
	 * @return Encoded string
	 */
	function encodeISOString( string str ) {
		return .encodeISOString( str, .defaultEncodingTarget );
	}
	/**
	 * @function encodeISOString
	 * @declaration function encodeISOString( string str, string charset )
	 * @brief Encode a string from a given character set into an ISO format
	 * @param string str The string to encode
	 * @param string charset The character set to assume the string came from
	 * @return The UTF-8 encoded ISO string.
	 */
	function encodeISOString( string str, string charset ) {
		if( not .canDecodeISOString(str) and not Regexp.match('^[A-Za-z0-9.? -:]+$', str ) ) {
			string real_str = str;
			string enc_str = '';
			if( charset != .defaultEncodingTarget ) {
				real_str = .convertToDefaultEncodingFrom( str, charset );
			}
			real_str.blocks(50).each() using ( block ) {
				enc_str += Mail.Connection.bit8( block );
			};		
		    enc_str = Regexp.replaceAll( '\ ', enc_str, '_' );
		    enc_str = Regexp.replaceAll( '\?', enc_str, '=3F' );
			if( real_str )
			    return "=?${.defaultEncodingTarget}?Q?$enc_str?=";
			return "";
		} 
		return str;
	}
	/**
	 * @function stripHTML
	 * @declaration function stripHTML( string html )
	 * @brief Strip the HTML from a given string
	 * @description This function will convert various entities to plain text versions, remove tags and replaces line breaks with new lines.
	 * @param string html The string to strip
	 * @return A stripped string
	 */
	function stripHTML( string html ) {
	    number i = 0;
	    string result; 
	    array entities = [ ["nbsp"," "],[ "amp","&"], ["apos","'"],["lt","<"],["lt","<"], ["gt",">"],["lt","<"],["lt","<"] ];

	    object regex = new Regexp("<br ?.*?>", "gxi");
	    html = regex.replaceAll(html, "\n");

	    result = self.stripTags(html);
	    if(result != html ) {
			for(i = 0; i < Array.size(entities); i++) {
			    regex = new Regexp("(&" + entities[i][0] + ";)", "igx");
			    result = regex.replaceAll(result, entities[i][1]);
			}
	    }
	    return result;
	}

	native function stripTags(string html) {
	    return strip_tags(script, html->data, html->length);
	}
	
	/**
	 * @class Connection
	 * @brief An instance of this class is required to connect to a mail server
	 */
	class Connection
	{
		string _errstr;
		string account_type; 

		native function constructor() {
			self->odata = NULL;
			//set timeout.
			mail_parameters(NIL, SET_OPENTIMEOUT, (void *)20);
			mail_parameters(NIL, SET_READTIMEOUT, (void *)10);
			mail_parameters(NIL, SET_WRITETIMEOUT, (void *)10);
			mail_parameters(NIL, SET_CLOSETIMEOUT, (void *)10);
			//set number of login tries.
			mail_parameters(NIL, SET_MAXLOGINTRIALS, (void *)2);

			//mail_parameters(NIL, SET_TRYSSLFIRST, (void *)T);
			//mail_parameters(NIL, SET_IMAPTRYSSL, (void *)NIL);
		}
		
		function setAccountType(string type) 
		    self.account_type = type;

		function errstr() {
			return ._errstr;
		}

		/**
		 * @function qprint
		 * @declaration static native function qprint( string str )
		 * @brief Convert a normal string into a quoted printable string
		 * @param string str The string to convert
		 * @return A quoted printable version of the string
		 * @static
		 */
		static native function qprint( string str ) {
			FeriteVariable *v;	
			char *nstr;
			unsigned long len2;
			//fprintf(stderr, "str = %s, len = %d", str->data, str->length);
			nstr = rfc822_qprint( str->data , str->length, &len2 );
			//fprintf(stderr, "New string=%s len2 = %d", nstr, len2);
			v = fe_new_str( "qprint", nstr, len2, FE_CHARSET_DEFAULT );
			FE_RETURN_VAR( v );
		}

		/**
		 * @function base64
		 * @declaration static native function base64( string str )
		 * @brief Base64 encode a string
		 * @param string str The string to base64 encode
		 * @return The string in base64 format
		 * @static
		 */
		static native function base64( string str ) {
			FeriteVariable *v;	
			char *nstr;
			unsigned long len2;
			//fprintf(stderr, "str = %s, len = %d", str->data, str->length);
			nstr = rfc822_base64( str->data , str->length, &len2 );
			//fprintf(stderr, "New string=%s len2 = %d", nstr, len2);
			v = fe_new_str( "base64", nstr, len2, FE_CHARSET_DEFAULT );
			FE_RETURN_VAR( v );
		}
		/**
		 * @function bit8
		 * @declaration static native function bit8( string str )
		 * @brief Using the RFC822 8bit specification, convert a string to be 8bit safe.
		 * @param string str The string to convert
		 * @return 8bit safe string
		 * @static
		 */
		static native function bit8( string str ) {
			FeriteVariable *v;	
			char *nstr;
			unsigned long len2;
			//fprintf(stderr, "str = %s, len = %d", str->data, str->length);
			nstr = rfc822_8bit( str->data , str->length, &len2 );
			//fprintf(stderr, "New string=%s len2 = %d", nstr, len2);
			v = fe_new_str( "8bit", nstr, len2, FE_CHARSET_DEFAULT );
			FE_RETURN_VAR( v );
		}

		/**
		 * @function openMailbox
		 * @declaration function openMailbox( string proto, string host, number port, string username, string password )
		 * @brief Connect and open the default mailbox readed for reading
		 * @param string proto The protocol (imap or pop3)
		 * @param string host The host to connect to
		 * @param number port The port to use (default pop3: 110, imap: 143 and imaps: 993)
		 * @param string username The username to log in with
		 * @param string password The password to use
		 * @return true if connection successful, false otherwise
		 */
		function openMailbox( string proto, string host, number port, string username, string password ) {
			return .openSpecificMailbox( proto, host, port, username, password, "INBOX" );
		}
		/**
		 * @function openSpecificMailbox
		 * @declaration function openSpecificMailbox( string proto, string host, number port, string username, string password, string mailbox )
		 * @brief Connect to a server and open the specifically named mailbox for reading. If you supply the name "INBOX", the mail server will use the default inbox for the user.
		 * @param string proto The protocol to use (imap/pop3)
		 * @param string host The host to connec to 
		 * @param number port The port to use (default pop3: 110, imap: 143 and imaps: 993)
		 * @param string username The user to log in with
		 * @param string password The passowrd to use
		 * @param string mailbox The mailbox to connect to
		 * @return true if connection successful, false otherwise
		 */
		function openSpecificMailbox( string proto, string host, number port, string username, string password, string mailbox ) {
			return .openMailboxURI( "{$host:$port/$proto/user=\"$username\"}$mailbox", password );
		}
		/**
		 * @function openMailboxURI
		 * @declaration native function openMailboxURI( string mbox, string passwd )
		 * @brief Connect and open a given mailbox identifier
		 * @description The mbox identifier can be made up of the following values:<nl/>
		 * <nl/>
		 * The mbox part, which is enclosed in '{' and '}', consists of the servers name or ip address, an optional port (prefixed by ':'), and an optional protocol specification (prefixed by '/'). All names which start with { are remote names, and are in the form "{" remote_system_name [":" port] [flags] "}" [mailbox_name] where:<nl/>
		 * <nl/>
		 * <emphasis>remote_system_name</emphasis> - Internet domain name or bracketed IP address of server.<nl/>
		 * <emphasis>port</emphasis> - optional TCP port number, default is the default port for that service<nl/>
		 * <emphasis>flags</emphasis> - optional flags, see following details.<nl/>
		 * <emphasis>mailbox_name</emphasis> - remote mailbox name, default is INBOX<nl/>
		 * <nl/>
		 * <strong>/service=service</strong> : mailbox access service, default is "imap"<nl/>
		 * <strong>/user=user</strong> : remote user name for login on the server<nl/>
		 * <strong>/authuser=user</strong> : remote authentication user; if specified this is the user name whose password is used (e.g. administrator)<nl/>
		 * <strong>/anonymous</strong> : remote access as anonymous user<nl/>
		 * <strong>/debug</strong> : record protocol telemetry in application's debug log<nl/>
		 * <strong>/secure</strong> : do not transmit a plaintext password over the network<nl/>
		 * <strong>/imap, /imap2, /imap2bis, /imap4, /imap4rev1</strong> : equivalent to /service=imap<nl/>
		 * <strong>/pop3</strong> : equivalent to /service=pop3<nl/>
		 * <strong>/nntp</strong> :equivalent to /service=nntp<nl/>
		 * <strong>/norsh</strong> : do not use rsh or ssh to establish a preauthenticated IMAP session<nl/>
		 * <strong>/ssl</strong> :use the Secure Socket Layer to encrypt the session<nl/>
		 * <strong>/validate-cert</strong> : validate certificates from TLS/SSL server (this is the default behavior)<nl/>
		 * <strong>/novalidate-cert</strong> : do not validate certificates from TLS/SSL server, needed if server uses self-signed certificates<nl/>
		 * <strong>/tls</strong> : force use of start-TLS to encrypt the session, and reject connection to servers that do not support it<nl/>
		 * <strong>/notls</strong> : do not do start-TLS to encrypt the session, even with servers that support it<nl/>
		 * <strong>/readonly</strong> :request read-only mailbox open (IMAP only; ignored on NNTP, and an error with SMTP and POP3)<nl/>
		 * @example openMailbox("{host:993/imap/ssl/novalidate-cert/user="username"}INBOX", "password")
		 * @param string mbox A description as above.
		 * @param string passwd The password
		 * @return rue if connection successful, false otherwise
		 */
		native function openMailboxURI( string mbox, string passwd ) {
			char data[1024];
#include "imap/linkage.c" 
			global_pwd = passwd->data;

			self->odata = mail_open( SelfObj, mbox->data, 0);
			if( SelfObj == NULL ) {
				set_error_string( script, self, "Unable to open mailbox" );
				FE_RETURN_FALSE;
			}
			FE_RETURN_TRUE;
		}

		/**
		 * @function closeMailbox
		 * @declaration native function closeMailbox( )
		 * @brief Close the mailbox if it is open
		 * @return true if the mail box has been closed, false otherwise
		 */
		native function closeMailbox( ) {
			if( SelfObj ) {
				mail_close( SelfObj );
				self->odata = NULL;
				FE_RETURN_TRUE;
			}
			FE_RETURN_FALSE;
		}
		
		/**
		 * @function messageCount
		 * @declaration native function messageCount( )
		 * @brief Read the mailbox and return the number of messages within the mail box.
		 * @return Message count as a number
		 */
		native function messageCount( ) {
			FE_RETURN_LONG( SelfObj->nmsgs );
		}

		/**
		 * @function messageAtIndex
		 * @declaration native function messageAtIndex( number n )
		 * @brief Fetch an entire message at a given index.
		 * @warning All indicies are not like normal array indicies, the count starts at 1 and ends at .messageCount()
		 * @param number n The index to fetch
		 * @return A Mail.Message object on success, null otherwise
		 */
		native function messageAtIndex( number n ) {
			ENVELOPE *env = NULL;
			BODY *body = NULL;
			FeriteVariable *header = NULL, *content = NULL, *mail = NULL;
			
			if( (long)n < 1 || (long)n > SelfObj->nmsgs ) {
				set_error_string( script, self, "Message number out of bounds" );
				FE_RETURN_NULL_OBJECT;
			}

			env = mail_fetchstructure_full( SelfObj, n, &body, NIL );

			header = create_ferite_header_object( script, env );
			if( header == NULL ) {
				set_error_string( script, self, "Couldn't create header" );
				FE_RETURN_NULL_OBJECT;
			}

			content = create_ferite_content_object( script, SelfObj, body, (long)n, NULL );
			if( content == NULL ) {
				set_error_string( script, self, "Couldn't create body" );
				FE_RETURN_NULL_OBJECT;
			}

			if( header == NULL || body == NULL ) {
				FE_RETURN_NULL_OBJECT;
			}
			mail = create_ferite_mail_object( script, header, content );
			if( mail == NULL ) {
				set_error_string( script, self, "Internal error" );
				FE_RETURN_NULL_OBJECT;
			} else {
				unsigned long header_length = 0, body_length = 0;
				char *raw_header = mail_fetchheader_full( SelfObj, n, NULL, &header_length, 0 );
				char *raw_body = mail_fetchtext_full( SelfObj, n, &body_length, 0 );
				FeriteVariable *rawheaderv = ferite_object_get_var( script, VAO(mail), "rawHeader" );
				FeriteVariable *rawcontentv = ferite_object_get_var( script, VAO(mail), "rawContent" );
				ferite_str_set( script, VAS(rawheaderv), raw_header, header_length, FE_CHARSET_DEFAULT );
				ferite_str_set( script, VAS(rawcontentv), raw_body, body_length, FE_CHARSET_DEFAULT );
			}
			
			FE_RETURN_VAR( mail );
		}
		/**
		 * @function messageHeaderAtIndex
		 * @declaration native function messageHeaderAtIndex( number n )
		 * @brief Fetch just the message header at a given index.
		 * @warning All indicies are not like normal array indicies, the count starts at 1 and ends at .messageCount()
		 * @param number n The index to fetch
		 * @return A Mail.Message object on success with the header variable set, null otherwise
		 */
		native function messageHeaderAtIndex( number n ) {
			ENVELOPE *env = NULL;
			BODY *body = NULL;
			FeriteVariable *header = NULL, *content = NULL, *mail = NULL;
			unsigned long length = 0;
			
			if( (long)n < 1 || (long)n > SelfObj->nmsgs ) {
				set_error_string( script, self, "Message number out of bounds" );
				FE_RETURN_NULL_OBJECT;
			}

			env = mail_fetchenvelope( SelfObj, n );
			header = create_ferite_header_object( script, env );
			if( header == NULL ) {
				set_error_string( script, self, "Couldn't create header" );
				FE_RETURN_NULL_OBJECT;
			}
			mail = create_ferite_mail_object( script, header, content );
			if( mail == NULL ) {
				set_error_string( script, self, "Internal error" );
				FE_RETURN_NULL_OBJECT;
			} else {
				char *raw_header = mail_fetchheader_full( SelfObj, n, NULL, &length, 0 );
				FeriteVariable *rawheaderv = ferite_object_get_var( script, VAO(mail), "rawHeader" );
				ferite_str_set( script, VAS(rawheaderv), raw_header, length, FE_CHARSET_DEFAULT );
			}
			
			FE_RETURN_VAR( mail );
		}

		/**
		 * @function sendMessage
		 * @declaration native function sendMessage( string server, object mailet )
		 * @brief With a given Mail.Message object, send the email using the supplied SMTP server
		 * @warning This function will automatically ISO encode headers, names and the subject of the email if it needs to be done.
					The change will not be reflected when the function has returned. This means you need not worry about correct ISO
					string escaping for the various parts of the email - just assign strings and away you go.
		 * @param string server The server to send the message with
		 * @param object mailet The Mail.Message object to send
		 * @return true on success, fales otherwise
		 */
		function sendMessage( string server, object mail ) {
			object old_header = mail.header;
			object new_header = old_header.duplicate();
			string new_subject = Mail.encodeISOString(old_header.subject);
			boolean result = false;
			// Encode the headers
			mail.outgoingHeaders.map() using ( key, value ) {
				new_subject += "\r\n$key: ${Mail.encodeISOString(value)}";
			};
			new_header.subject = new_subject;
			// Check the encoding of names
			new_header.checkEncoding();
			// Swap out the old header
			mail.header = new_header;
			result = .__sendMessage( server, mail );
			// Bring back the old header
			mail.header = old_header;
			return result;
		}
		native function __sendMessage( string server, object mailet ) {
			FeriteVariable *content, *header;
			ENVELOPE *env;
			BODY *body;
			SENDSTREAM *stream;

			char *hostlist[] = { server->data, NIL };
			
			header  = ferite_hash_get(script,mailet->variables->variables,"header");
			content = ferite_hash_get(script,mailet->variables->variables,"content");
			if(! ( header && content ) ) {
				printf("internal error\n");
				FE_RETURN_FALSE;
			}
			
			body = create_imap_content_object(script, content);
			if ( body == NULL ) {
				set_error_string( script, self, "Couldn't create body" );
				FE_RETURN_FALSE;
			}

			env = create_imap_envelope( script, header );
			if ( env == NULL ) {
				set_error_string( script, self, "Couldn't create header" );
				FE_RETURN_FALSE;
			}

			stream = smtp_open( hostlist, 0); //SOP_TRYSSL
 
			if( stream == NULL ) {
				set_error_string( script, self,"Error  opening SMTP stream" );
				FE_RETURN_FALSE;
			}

	
			if( smtp_mail( stream, "MAIL", env, body ) == NIL ) {
				set_error_string( script, self,"SMTP Error" );
				FE_RETURN_FALSE;
			}
			smtp_close( stream );
			FE_RETURN_TRUE;
		}
		
		/**
		 * @function expungeMessages
		 * @declaration native function expungeMessages()
		 * @brief Expunge all messages flagge as being deleted 
		 */
		native function expungeMessages() {
			mail_expunge( SelfObj );
			FE_RETURN_TRUE;
		}

		/**
		 * @function flagMessageDeleted
		 * @declaration native function flagMessageDeleted( number n )
		 * @brief Flag a message at a given index to be deleted. Use expungeMessages() to actually remove all the messages.
		 * @warning All indicies are not like normal array indicies, the count starts at 1 and ends at .messageCount()
		 * @param number n The message index to be flagged
		 */
		native function flagMessageDeleted( number n ) {
			char str[9];
			if( n > 0 && n <= SelfObj->nmsgs ) { 
				sprintf (str,"%lu",(long)n);
				mail_setflag_full(SelfObj, str, "\\DELETED", NIL);
				FE_RETURN_TRUE;
			}
			FE_RETURN_FALSE;
		}

		/**
		 * @function flagMessageSeen
		 * @declaration native function flagMessageSeen( number n )
		 * @brief Flag a message at a given index as being seen.
		 * @warning All indicies are not like normal array indicies, the count starts at 1 and ends at .messageCount()
		 * @param number n The message index to be flagged
		 */
		native function flagMessageSeen( number n ) {
			char str[7];
			if( n > 0 && n <= SelfObj->nmsgs ) { 
				sprintf(str,"%lu",(long)n);
				mail_setflag_full(SelfObj,str,"\\SEEN", NIL);
				FE_RETURN_TRUE;
			}
			FE_RETURN_FALSE;
		}
		/**
		 * @function messageBeenSeen
		 * @declaration native function messageBeenSeen( number n )
		 * @brief Check to see if a message at a given index has been seen.
		 * @warning All indicies are not like normal array indicies, the count starts at 1 and ends at .messageCount()
		 * @param number n The message index to check
		 * @return true if the message has been seen, false otherwise.
		 */
		native function messageBeenSeen( number n ) {
		    MESSAGECACHE *elt;
		    if( n > 0 && n <= SelfObj->nmsgs ) {
				elt  = mail_elt(SelfObj, n);
				if( elt->seen ) { // elt->recent)
			    	FE_RETURN_TRUE;
				}
		    }
		    FE_RETURN_FALSE;
		}
	}
	/**
	 * @end
	 */

	
	/**
	 * @class Message
	 * @brief Every email is represented by a Mail.Message object
	 */
 	class Message
	{
		/**
		 * @variable header
		 * @type     object
		 * @brief    A variable of type Mail.MessageHeader
		 */
		object header;
		/**
		 * @variable content
		 * @type     object
		 * @brief    An object of either Mail.MessageMultiPart or Mail.MessagePart
		 * @static
		 */
		object content;

		/**
		 * @variable rawHeader
		 * @type     string
		 * @brief    The complete raw header for the message
		 */
		string rawHeader;
		/**
		 * @variable rawContent
		 * @type     string
		 * @brief    The complete raw content for the message
		 * @warning  Be careful using this variable, it could be multiple megabytes big which can be very slow to process.
		 */
		string rawContent;

		array extractedHeaders;
		/**
		 * @function extractIncomingHeaders
		 * @declaration function extractIncomingHeaders()
		 * @brief Extract the custom headers from the mail header and return a keyed array with the values
		 * @return The headers in an associative array
		 */
		function extractIncomingHeaders() {
			if( not .extractedHeaders ) {
				array s = String.dissect( Regexp.replaceAll("\r", .rawHeader, ""), "\n", 0 );
				string lastHeader;
				object m, r = new Regexp( '^([^:]+): (.*)' );
				s.each() using ( line ) {
					if( (m = r.match(line)) ) {
						lastHeader = m.capture(0);
						.extractedHeaders[lastHeader] = m.capture(1).trim(' ');
					} else {
						.extractedHeaders[lastHeader] += ' ' + line.trim(' ');
					}
				};
			}
			return .extractedHeaders;
		}
		
		array outgoingHeaders;
		/**
		 * @function setOutgoingHeader
		 * @declaration function setOutgoingHeader( string name, string value )
		 * @brief Set a custom out-going header to be added to the flags
		 * @param string name The name of the header, if unofficial prefix it with 'X-'
		 * @param string value The value of the header
		 */
		function setOutgoingHeader( string name, string value ) {
			.outgoingHeaders[name] = value;
		}
		
		function constructor(){
			self.header = new Mail.MessageHeader();
		}
		
		/**
		 * @function getSubject
		 * @declaration function getSubject()
		 * @brief Get the message subject. This function will make sure that the string returned has been processed.
		 * @return The subject
		 */
		function getSubject() {
			string subject = (.header.subject ? .header.subject : '');
			if( subject ){
				if( Mail.canDecodeISOString(subject) ) {
					subject = Mail.decodeISOString(subject);
				} else if( .content ) {
					subject = Mail.convertToDefaultEncodingFrom( subject, .guessMainCharacterSet() );
				}
			}
			return subject;
		}
		/**
		 * @function guessMainCharacterSet
		 * @declaration function guessMainCharacterSet()
		 * @brief Guess, by looking at the message content, what the default character set of the incomming email was.
		 * @return The character set as a string.
		 */
		function guessMainCharacterSet() {
			return .content.characterSet();
		}
		/**
		 * @function attachmentList
		 * @declaration function attachmentList()
		 * @brief Fetch an array of Mail.MessagePart(s) that are attachments to the email
		 * @return An array of attachments.
		 */
		function attachmentList()
			return self.content.attachmentList();
			
		/**
		 * @function toText
		 * @declaration function toText()
		 * @brief The email as plain text. This will be a combination of the text/plain elements of the email.
		 * @description You may pass this function a closure that takes one parameter - the Mail.MessagePart object that represents an attachment. 
				The return from invoking this closure will be the content of the attachment in the email. This is useful when you desire custom anchors
				or other functionality in the place of an attachment. If you do not supply a closure, the system will place attachment anchors within the
				returned string, to retrieve these and do some processing, you need to use the function Mail.MessagePart.regexpForAttachmentAnchors 
				to obtain an object that can be used to match the anchors within the text.
		 * @return A string containing the message body
		 */
		function toText() {
			object attachmentDelivery = recipient();
			return self.content.toMessage( false, attachmentDelivery );
		}
		/**
		 * @function toHTML
		 * @declaration function toHTML()
		 * @brief The email as html. This will be a combination of the text/html elements of the email.
		 * @description You may pass this function a closure that takes one parameter - the Mail.MessagePart object that represents an attachment. 
				The return from invoking this closure will be the content of the attachment in the email. This is useful when you desire custom anchors
				or other functionality in the place of an attachment. If you do not supply a closure, the system will place attachment anchors within the
				returned string, to retrieve these and do some processing, you need to use the function Mail.MessagePart.regexpForAttachmentAnchors 
				to obtain an object that can be used to match the anchors within the text.
		 * @return The return values
		 */
		function toHTML() {
			object attachmentDelivery = recipient();
			return self.content.toMessage( true, attachmentDelivery );
		}
		/**
		 * @function hasTextVersion
		 * @declaration function hasTextVersion()
		 * @brief Check to see if the email has a text/plain part(s) to it.
		 * @return true if there is a text/plain block, false otherwise
		 */
		function hasTextVersion()
			return self.content.hasTextVersion();
		/**
		 * @function hasHTMLVersion
		 * @declaration function hasHTMLVersion()
		 * @brief Check to see if the email has a text/html part(s) to it.
		 * @return true if there is a text/html block, false otherwise
		 */
		function hasHTMLVersion()
			return self.content.hasHTMLVersion();
	}
	/**
	 * @end
	 */

	/**
	 * @class Address
	 * @brief A point of contact. So to speak.
	 */
	class Address {
		
        /**
         * @function constructor
         * @declaration function constructor( string mailbox, string host )
         * @brief Create a Mail.Address object from a given mailbox name and host
         * @param string mailbox The mailbox name e.g. user
         * @param string host The host name e.g. cention.se
         */
		function constructor( string mailbox, string host ){
			self.mailbox = mailbox;
			self.host = host;
		}
		/**
		 * @function constructor
		 * @declaration function constructor( string addr )
		 * @brief Create a Mail.Address object from a given email address
		 * @param string addr The email address
		 */
		function constructor( string addr ) {
			array hits;
			object result;
			object regex = new Regexp("(.*?)\@(.*)", "i");

			result = regex.match( addr );
			hits = result.captures();

			if( hits && hits[0] && hits[1] ) {
				self.mailbox = hits[0];
				self.host = hits[1];
			}
			raise new Error("Invalid email address passed to Mail.Address constructor");
		}

		/**
		 * @variable mailbox
		 * @type     string
		 * @brief    The name of the user for the mail box
		 */
		string mailbox;
		/**
		 * @variable host
		 * @type     string
		 * @brief    The email host
		 */
		string host;
		/**
		 * @variable name
		 * @type     string
		 * @brief    The real world name of the user. Please use .flatName() to access this value
		 */
		string name;
		
		/**
		 * @function flatName
		 * @declaration function flatName()
		 * @brief The name of the user. This function will make sure that the name is fixed and converted into plain text	
		 * @return The name of the user
		 */
		function flatName()
			return Mail.decodeISOString(.name);
		/**
		 * @function flatAddress
		 * @declaration function flatAddress()
		 * @brief The combined email address as a string
		 * @return See the brief.
		 */
		function flatAddress()
			return "${.mailbox}@${.host}";
			
		function duplicate() {
			object o = new Mail.Address();
			o.mailbox = .mailbox;
			o.host = .host;
			o.name = .name;
			return o;
		}
		function checkEncoding() {
			.name = Mail.encodeISOString(.name);
		}
	}
	/**
	 * @end
	 */
	
	/**
	 * @class AddressList
	 * @brief A list of addresses
	 */
	class AddressList {
		/**
		 * @variable list
		 * @type     array
		 * @brief    The Mail.Address list
		 */
		array list;

		/**
		 * @function add
		 * @declaration function add(object address)
		 * @brief Add a Mail.Address object to the list
		 * @param object address The object to add
		 */
		function add(object address){
			Array.push( self.list, address );
		}
		/**
		 * @function flatAddresses
		 * @declaration function flatAddresses()
		 * @brief Get an array of string representing the flatAddress() of each Mail.Address object
		 * @description A description
		 * @return The return values
		 */
		function flatAddresses() {
			array list = [];
			.list.each() using ( address ) {
				list[] = address.flatAddress();
			};
			return list;
		}
		function duplicate() {
			object o = new Mail.AddressList();
			.list.each() using ( address ) {
				o.add( address.duplicate() );
			};
			return o;
		}
		function checkEncoding() {
			.list.each() using ( address ) {
				address.checkEncoding();
			};
		}
	}
	/**
	 * @end
	 */
	
	/**
	 * @class MessageHeader
	 * @brief The header of a Mail.Message
	 */
	class MessageHeader {
		/**
		 * @variable to
		 * @type     Mail.AddressList
		 * @brief    The list of people the email was/should be sent to.
		 */
		object to;
		/**
		 * @variable cc
		 * @type     Mail.AddressList
		 * @brief    The list of people the email was/should be CC'd to
		 */
		object cc;
		/**
		 * @variable bcc
		 * @type     Mail.AddressList
		 * @brief    The list of people the email should be BCC'd to
		 */
		object bcc;
		/**
		 * @variable reply_to
		 * @type     Mail.AddressList
		 * @brief    The list of people the email was/should be replied to
		 */
		object reply_to;
		/**
		 * @variable from
		 * @type     Mail.AddressList
		 * @brief    An array containing a single Mail.Address that the email is/was from
		 */
		object from;
		/**
		 * @variable sender
		 * @type     Mail.AddressList
		 * @brief    The sender list
		 */
		object sender;
		
		/**
		 * @variable ID
		 * @type     string
		 * @brief    The message ID (should be unique)
		 */
		string ID;
		/**
		 * @variable in_reply_to
		 * @type     string
		 * @brief    The ID the email is in reply to. Allows the tracking of threads.
		 */
		string in_reply_to;
		/**
		 * @variable date
		 * @type     string
		 * @brief    The date the message was created
		 */
		string date;
		/**
		 * @variable recieved
		 * @type     string
		 * @brief    The date the message was received
		 */
		string recieved;
		/**
		 * @variable subject
		 * @type     string
		 * @brief    The message subject
		 */
		string subject;

		function constructor(){
			self.to = new Mail.AddressList();
			self.from = new Mail.AddressList();
			self.bcc = new Mail.AddressList();
			self.reply_to  = new Mail.AddressList();
			self.cc = new Mail.AddressList();
			self.sender = new Mail.AddressList();
		}
		function duplicate() {
			object o = new Mail.MessageHeader();
			o.from = .from.duplicate();
			o.sender = .sender.duplicate();
			o.to = .to.duplicate();
			o.cc = .cc.duplicate();
			o.bcc = .bcc.duplicate();
			o.reply_to = .reply_to.duplicate();
			o.ID = .ID;
			o.in_reply_to = .in_reply_to;
			o.date = .date;
			o.recieved = .recieved;
			o.subject = .subject;
			return o;
		}
		function checkEncoding() {
			.from.checkEncoding();
			.sender.checkEncoding();
			.to.checkEncoding();
			.cc.checkEncoding();
			.bcc.checkEncoding();
			.reply_to.checkEncoding();
		}
	}
	/**
	 * @end
	 */

	/**
	 * @class MessageMultiPart
	 * @brief A multipart container for a email block
	 */
	class MessageMultiPart
	{
		array  parts;
		number nparts;
		number type;
		string subtype;
		
		function constructor(){
			self.type = Mime.TYPEMULTIPART;
		}
		
		/**
		 * @function isMultipart
		 * @declaration function isMultipart()
		 * @brief Check to see if the given block is a multipart block
		 * @return true if multipart, false otherwise.
		 */
		/**
		 * @function characterSet
		 * @declaration function characterSet()
		 * @brief Guess the character set of the tree of parts
		 * @return The character set, empty string otherwise
		 */
		/**
		 * @function toMessage
		 * @declaration function toMessage( boolean wantHTML )
		 * @brief Fetch all the parts below and including this part
		 * @param boolean wantHTML Whether html parts are required. true - only text/html is returned. false - only text/plain is returned.
		 * @param object attachmentDelivery A closure to invoke when an attachment object is found when generating the message.
		 * @return The message body
		 */
		/**
		 * @function attachmentList
		 * @declaration function attachmentList()
		 * @brief Fetch an array of Mail.MessagePart(s) that are attachments to the email
		 * @return An array of attachments.
		 */
		/**
		 * @function hasTextVersion
		 * @declaration function hasTextVersion()
		 * @brief Check to see if the part has a text/plain part(s) to it.
		 * @return true if there is a text/plain block, false otherwise
		 */
		/**
		 * @function hasHTMLVersion
		 * @declaration function hasHTMLVersion()
		 * @brief Check to see if the part has a text/html part(s) to it.
		 * @return true if there is a text/html block, false otherwise
		 */
		function isMultipart(){
			return true;
		}
		function characterSet() {
			string set = '';
			.parts.each() using ( part ) {
				set = part.characterSet();
				if( set )
					return false;
			};
			return set;
		}
		function toMessage( boolean wantHTML, object attachmentDelivery ) {
			return .parts.join('') using ( part ) {
				return part.toMessage( wantHTML, attachmentDelivery );
			};
		}
		function attachmentList() {
			array list = [];
			.parts.each() using ( part ) {
				array subList = part.attachmentList();
				subList.each() using ( subpart ) {
					list[] = subpart;
				};
			};
			return list;
		}
		
		function hasTextVersion() {
			boolean result = false;
			.parts.each() using ( part ) {
				if( part.hasTextVersion() ) {
					result = true;
					return false;
				}
			};
			return result;
		}
		function hasHTMLVersion() {
			boolean result = false;
			.parts.each() using ( part ) {
				if( part.hasHTMLVersion() ) {
					result = true;
					return false;
				}
			};
			return result;
		}
		/**
		 * @function addPart
		 * @declaration function addPart(object sub)
		 * @brief Add a Mail.MessagePart or Mail.MessageMultiPart to this block
		 * @param object sub The sub-part to add
		 */
		function addPart(object sub) {
			Array.push(self.parts,sub);
		}
		/**
		 * @function addAttachment
		 * @declaration function addAttachment(string path)
		 * @brief Add a given file as a part as an attachment
		 * @param string path The path to the file
		 */
		function addAttachment(string path){
		    object regex;
		    object match;
			//Fixme check that file is readable
			object part = new Mail.MessagePart();
			part.filepath = path;
			part.type = Mail.Mime.TYPEAPPLICATION;
			part.subtype = "octet-stream";
			part.encoding = Mail.Encoding.ENCBINARY;

			regex = new Regexp(".*/([^/]+*)$");
			if( (match = regexp.match(path)) )
				part.filename = match.match();
			else
				part.filename = path; 
			Array.push(self.parts,part);
		}
	}
	/**
	 * @end
	 */
	
	/**
	 * @class MessagePart
	 * @brief An inidividual content part of an email block
	 */
	class MessagePart
	{
		number type;
		string subtype;
		number encoding;
		string content;
		string charset;
		string filename;
		string filepath;
		
		function constructor( ) {
			self.type = Mail.Mime.TYPETEXT;
			self.charset = 'UTF-8';
		}

		/**
		 * @function isMultipart
		 * @declaration function isMultipart()
		 * @brief Check to see if the given block is a multipart block
		 * @return true if multipart, false otherwise.
		 */
		/**
		 * @function characterSet
		 * @declaration function characterSet()
		 * @brief Guess the character set of the tree of parts
		 * @return The character set, empty string otherwise
		 */
		/**
		 * @function toMessage
		 * @declaration function toMessage( boolean wantHTML )
		 * @brief Fetch all the parts below and including this part
		 * @param boolean wantHTML Whether html parts are required. true - only text/html is returned. false - only text/plain is returned.
		 * @param object attachmentDelivery A closure to invoke when an attachment object is found when generating the message.
		 * @return The message body
		 */
		/**
		 * @function attachmentList
		 * @declaration function attachmentList()
		 * @brief Fetch an array of Mail.MessagePart(s) that are attachments to the email
		 * @return An array of attachments.
		 */
		/**
		 * @function hasTextVersion
		 * @declaration function hasTextVersion()
		 * @brief Check to see if the part has a text/plain part(s) to it.
		 * @return true if there is a text/plain block, false otherwise
		 */
		/**
		 * @function hasHTMLVersion
		 * @declaration function hasHTMLVersion()
		 * @brief Check to see if the part has a text/html part(s) to it.
		 * @return true if there is a text/html block, false otherwise
		 */		
		function isMultipart() {
			return false;
		}
		function characterSet() {
			return .charset;
		}
		/**
		 * @function regexpForAttachmentAnchors
		 * @declaration static function regexpForAttachmentAnchors { return new Regexp('attachment://([^/]*?)
		 * @brief A regular expression object that will match the attachment anchors provided when no attachment block was provided on a to-() function
		 * @return A regular expression object
		 * @static
		 */
		static function regexpForAttachmentAnchors {
			return new Regexp('attachment://([^/]*?)/type=([A-Za-z0-9-]+:[A-Za-z0-9-]+)/');
		}
		function toMessage( boolean wantHTML, object attachmentDelivery ) {
			if( .isAttachment() ) {
				if( attachmentDelivery )
					return attachmentDelivery.invoke( self );
			 	else
					return "attachment://${.fileName()}/type=${Mail.Mime.toString(.type)}:${.subtype}/";
			} else if( .type == Mail.Mime.TYPETEXT ) {
				string content = Mail.convertToDefaultEncodingFrom( .content, .charset );
				if( wantHTML and .subtype.toLower() == 'html' ) {
					return content;
				} else if( not wantHTML and .subtype.toLower() == 'plain' ) {
				    return Regexp.replaceAll('<(.*@.*?)>', content, '\1');
				}
			}
			return '';
		}
		
		function attachmentList() {
		 	if( .isAttachment() ) {
				return [ self ];
			}
			return [];
		}
		function hasTextVersion() {
			if( .type == Mail.Mime.TYPETEXT and .subtype.toLower() == 'plain' )
				return true;
			return false;
		}
		function hasHTMLVersion() {
			if( .type == Mail.Mime.TYPETEXT and .subtype.toLower() == 'html' )
				return true;
			return false;
		}
		/**
		 * @function isAttachment
		 * @declaration function isAttachment()
		 * @brief Check to see if the part is an attachment
		 * @return true if it is an attachment, false otherwise
		 */
		function isAttachment() {
			return (.filename != '');
		}
		/**
		 * @function fileName
		 * @declaration function fileName()
		 * @brief The filename if it is an attachment, post-processed to remove encoding
		 * @return The filename.
		 */
		function fileName() {
			if( .isAttachment() ) {
				string actual_name = Mail.decodeISOString(.filename);
				if( File.name(actual_name) ){
					actual_name = File.name(actual_name);
				}
				if( actual_name[0] == "." ) {
					actual_name = "unkwown$actual_name";
				}
				return actual_name;
			}
			return '';
		}
	}
	/**
	 * @end
	 */	
}
/**
 * @end
 */

uses "cmime";